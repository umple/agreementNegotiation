// This model is a metamodel for
// negotiation. It can be used for
// the development of negotiation tools
// or negotiation domain-specific
// languages

// All negotiations have as their
// objective the creation or update of
// an Agreement. This could be
// an international treaty, a
// commercial agreement for purchase
// and sale, a labour-management
// collective agreement, or anything 
// else
//
// Note that the title of the
// agreement is determined by
// the AgreementVersion, since it
// can change

class Agreement {
  // An internal ID that can be used by an API
  // to allow for queries about this
  // specific agreement, irrespective of version
  autounique internalAgreementID;
  
  // Agreements are updated over time
  // so the latest approved version is
  // actually the most important
  1 -- * AgreementVersion;
  
  displayColor orange;
}

// The legal entities involved in
// negotiation are the Parties
// and the Users who are involved
// in negotiation for one of the 
// parties
class LegalEntity {
  abstract;
  name;
  address;
  emailAddress;

  displayColor yellow;
}

// All negotiations involve at least
// Two parties. These could be
// sovereign states, corporations,
// unions or individuals
class Party {
  isA LegalEntity;
  0..1 -- * UserRole;

  displayColor yellow;
}

// A party has UserRoles. The reason for
// tracking these is so that we know
// who is making proposals, or signing
// of on parts of the agreement
// Some UserRoles such as Mediator
// may be not be associated with a specific
// Party
class UserRole {
  
  enum Role {
    ChiefNegotiator,
    MemberAtTable,
    Mediator,
    Arbitrator,
    Other
  }
  Role role;

  displayColor yellow;
}

// A user may have multiple roles
// For example they may be involved in multiple negotiations
// for different agreements
class User {
  isA LegalEntity;
  userid;

  1 -- * UserRole;
  
  displayColor yellow;
}


// A VisibilityControlledItem is
// something that only certain
// LegalEntitles are allowed to see
// These include Versions of Proposals
// or Clauses that may be incomplete or
// being prepared by one party; or Notes
class VisibilityControlledItem {
  abstract;
  
  // Internal ID of the item that can be
  // referred to by an API that needs to
  // query specify Versions or Notes.
  autounique internalItemID;
  
  // An item may or may not be tagged
  // with an author
  0..1 authoredItem -- 0..1 User author;
  
  // It may be that only one person
  // such as a person creating a draft
  // can currently see a version, or
  // maybe everybody in a party, or
  // maybe all parties, or maybe just
  // the chief negotiator and an
  // assistant
  *  controlledItem -- 1..* LegalEntity
    allowedToSee;
  
  displayColor darkgoldenrod;
}

// An Item Version contains
// a set of ClauseVersions
// (containedClauses)
//
// Its two subclasses represent
// either an edited version of the
// agreement as a whole, or else
// of one of the clauses.
//
// It may be informal, private to
// one party,
// presented at the negotiation table,
// or in some state of approval
//
// Any change to any contained
// clause results
// in a new ItemVersion even
// if it is a temporary edit
class ItemVersion {
  isA Version;
  abstract;
  
  // An AgreementVersion should have a
  // title but a ClauseVersion may
  // or may not have a
  // title to help identify it
  lazy title;
  
  // The changeDescription is
  // optional, but could be used to
  // identify information such as
  // that it was presented to a party
  // or is subject to ratification etc.
  changeDescription;
  
  0..1 container -> * ClauseVersion containedClauses;
  
  //* -- * Proposal acceptedProposals; // The proposals accepted prior to creation of this
  
  displayColor lightsalmon;
}

// An ItemVersion is made up
// of ClauseVersions, and the
// difference between ItemVersions
// is determined by the
// differences in the ClauseVersions
//
// A new ClauseVersion is created
// every time the id, title, text or
// subclauses are edited
//
// A ClauseVersion is only part of an
// agreement version if it is a
// subclause of another ClauseVersion
// and/or is a top level clause of an
// AgreementVersion
//
// Moving a ClauseVersion from one
// parent to another means changing
// both parents
class ClauseVersion {
  isA ItemVersion;

  // The ID of a clause will normally
  // be a number or a letter, or
  // for subclauses, a string of these,
  // often separated by dots.
  // It will be displayed to the user
  // it can be changed such as when
  // re-ordering, insertion of new
  // clauses or reparenting take place
  // A the top level, the version might
  // include a string such as 'Article'
  //
  // The title would appear after the id
  id;
  
  // The text is the essential legal
  // text of the clause
  // It ises Markdown format
  // To allow for italics etc.
  text; // Markdown format
  
  // A clause may or may not have lower
  // level clauses called subclauses.
  // This is maintained in the
  // containedClauses association
  // inherited from ItemVersion
  // Note that if we want to find the
  // parent we have to navigate down
  // from the top level 
  // AgreementVersion because otherwise
  // every change to a clause would
  // require creating new versions of
  // its subclauses
  
  displayColor lightsalmon;
}

// This is a special case of
// ItemVersion for the top level
// agreements
class AgreementVersion {
  isA ItemVersion;

  // Each agreement version is
  // between 2 or more parties
  // which are defined below
  * -- 2..* Party;
  
  // An agreement may have a
  // descriptive Version label that is
  // made public
  versionLabel;
  
  displayColor lightsalmon;
}

// Clauses are the core elements of
// an agreement
//
// Each time a clause is changed
// a new ClauseVersion is created.
// The history of a clause can
// be explored by reviewing its
// versions
class Clause {
  // The internalClauseID is used
  // to tie togetner all the versions
  // of a clause so their history
  // can be tracked even if the clause
  // is radically changed over time
  // This can be used by an API to refer
  // to a clause irrespective of version
  autounique internalClauseID;
  1 -- * ClauseVersion;
  
  displayColor lightcoral;
}

// A Note is information attached
// to a version or proposal
// Since it is visibility controlled
// it can be private to just one
// user, or to a party, or to
// everybody. 
//
// A note is not a legal part of the
// agreement, but may help people
// to record what others thing,
// or why the change is proposed.
// Sometimes old notes can be used
// to help resolve disputes
// about interpretation
class Note {
  isA VisibilityControlledItem;
  
  // A note is about something
  // a note can even be about another
  // note, such as a private
  // observation about some rationale
  * -- 1 VisibilityControlledItem
    subject;
  
  // Arbitrary text associated with
  // the item
  markdownText;
  
  // By default there is no need
  // to record a note type, so it
  // would be Unknown. But if the note
  // provides evidence or justification
  // it can be tagged as Rationale,
  // and if the other party has reacted
  // in some way, it can be tagged
  // as Feedback-FromOtherParty to
  // allow for better searches
  enum NoteType {
    Unknown,
    Rationale-Private,
    Rationale-ToReveal,
    Rationale-Revealed,
    Rationale-FromOtherparty,
    Feedback-FromOtherParty,
    Analysis-AboutOtherParty-Private,
    Feedback-ToReveal,
    Feedback-Revealed,
    Feedback-FromOtherParty,
    ProcessObservations,
    Data,
    Other
  }
  NoteType noteType;
  
  displayColor goldenrod;
}

// A Proposal is a set of ItemVersions
// that can be linked by 'and', 'or' or
// 'excusive or'
// 
// It represents ideas for changes to
// an agreement
//
// The simplest case could be a change
// to just one clause
//
// But many Proposals have multiple
// elements, which might be linked
// as follows
//
// Most sets of elements are linked
// by 'and' meaning a conjunct
//   a conjunct means that all 
//   changes are requested to be made
//   together
// 
// But some elements could be
// alternatives with exclusive or
// 'either this or that,
// but not both'.
//
// Others could be 'disjuncts'.
// 'Any of these ... it does
// not have to be all'
//
// A final approved proposal has to 
// be a conjunct, or just hava a single
// element

class Proposal {
  isA Version;
  
  // This enum describes the kind of
  // Proposal
  enum BooleanLink {
    conjunct,
    alternative,
    disjunct
  };
  BooleanLink booleanLink;
    
  // The elements can be
  // ClauseVersions or
  // Proposals (i.e. sub-proposals)
  0..1 -> * Version elements;
  
  displayColor cornflowerblue;
}

class Version { 
  isA VisibilityControlledItem;
  abstract;
  
  Date versionDate;
  Time versionTime;
  
  // Each version is a result of
  // changes to a previous version
  * -- 0..1 Version basedOn;

  displayColor lightsalmon;
}