// This model is a metamodel for
// negotiation. It can be used for
// the development of negotiation tools
// or negotiation domain-specific
// languages

// All negotiations have as their
// objective the creation or update of
// an Agreement. This could be
// an international treaty, a
// commercial agreement for purchase
// and sale, a labour-management
// collective agreement, or anything 
// else
class Agreement {
  // Note that the title of the
  // agreement is determined by
  // the AgreementVersion, since it
  // can change
  autounique internalID;
  
  // Each agreement is
  // between 2 or more parties
  // which are defined below
  * -- 2..* Party;
  
  // Agreements are updated over time
  // so the latest approved version is
  // actually the most important
  1 -- * AgreementVersion;
  
  displayColor orange;
}

// The legal entities involved in
// negotiation are the Parties
// and the Users who are involved
// in negotiation for one of the 
// parties
class LegalEntity {
  abstract;
  name;
  address;
  emailAddress;

  displayColor yellow;
}

// All negotiations involve at least
// Two parties. These could be
// sovereign states, corporations,
// unions or individuals
class Party {
  isA LegalEntity;
  1--1..* User;

  displayColor yellow;
}

// A party has Users. The reason for
// tracking these is so that we know
// who is making proposals, or signing
// of on parts of the agreement
class User {
  isA LegalEntity;
  
  enum Role {
    ChiefNegotiator,
    MemberAtTable,
    Other
  }
  Role role;
  
  userid;
  
  displayColor yellow;
}

// A VisibilityControlledItem is
// something that only certain
// LegalEntitles are allowed to see
// These include ItemVersions
// that may be incomplete or being
// prepared by one party
class VisibilityControlledItem {
  abstract;
  
  // An item nay or may not be tagged
  // with an author
  0..1 -- 0..1 User author;
  
  // It may be that only one person
  // such as a person creating a draft
  // can currently see a version, or
  // maybe everybody in a party, or
  // maybe all parties, or maybe just
  // the chief negotiator and an
  // assistant
  *  -- 1..* LegalEntity
    allowedToSee;
  
  displayColor darkgoldenrod;
}

// An Item Version contains
// a set of ClauseVersions
// (containedClauses)
//
// Its two subclasses represent
// either an edited version of the
// agreement as a whole, or else
// of one of the clauses.
//
// It may be informal, private to
// one party,
// presented at the negotiation table,
// or in some state of approval
//
// Any change to any contained
// clause results
// in a new ItemVersion even
// if it is a temporary edit
class ItemVersion {
  isA Version;
  abstract;
  
  // An AgreementVersion should have a
  // title but a ClauseVersion may
  // or may not have a
  // title to help identify it
  lazy title;
  
  // The changeDescription is
  // optional, but could be used to
  // identify information such as
  // that it was presented to a party
  // or is subject to ratification etc.
  changeDescription;
  
  0..1 container -> * ClauseVersion containedClauses;
  
  //* -- * Proposal acceptedProposals; // The proposals accepted prior to creation of this
  
  displayColor lightsalmon;
}

// An ItemVersion is made up
// of ClauseVersions, and the
// difference between ItemVersions
// is determined by the
// differences in the ClauseVersions
//
// A new ClauseVersion is created
// every time the id, title, text or
// subclauses are edited
//
// A ClauseVersion is only part of an
// agreement version if it is a
// subclause of another ClauseVersion
// and/or is a top level clause of an
// AgreementVersion
//
// Moving a ClauseVersion from one
// parent to another means changing
// both parents
class ClauseVersion {
  isA ItemVersion;

  // The ID of a clause will normally
  // be a number or a letter, or
  // for subclauses, a string of these
  // often separated by dots.
  // It will be displayed to the user
  // it can be changed such as when
  // re-ordering, insertion of new
  // clauses or reparenting take place
  // A the top level, the version mignt
  // include a string such as 'Article'
  //
  // The title would appear after the id
  id;
  
  // The text is the essential legal
  // text of the clause
  // It ises Markdown format
  // To allow for italics etc.
  text; // Markdown format
  
  // A clause may or may not have lower
  // level clauses called subclauses.
  // This is maintained in the
  // containedClauses association
  // inherited from ItemVersion
  // Note that if we want to find the
  // parent we have to navigate down
  // from the top level 
  // AgreementVersion because otherwise
  // every change to a clause would
  // require creating new versions of
  // its subclauses
  
  displayColor lightsalmon;
}

// This is a special case of
// ItemVersion for the top level
// agreements
class AgreementVersion {
  isA ItemVersion;
  
  // An agreement may have a
  // descriptive Version label that is
  // made public
  versionLabel;
  
  displayColor lightsalmon;
}

// Clauses are the core elements of
// an agreement
//
// Each time a clause is changed
// a new ClauseVersion is created.
// The history of a clause can
// be explored by reviewing its
// versions
class Clause {
  // The internalID is used
  // to tie togetner all the versions
  // of a clause so their history
  // can be tracked even if the clause
  // is radically changed over time
  autounique internalID;
  1 -- * ClauseVersion;
  
  displayColor lightcoral;
}



// A Note is information attached
// to a version or proposal
// Since it is visibility controlled
// it can be private to just one
// user, or to a party, or to
// everybody. 
//
// A note is not a legal part of the
// agreement, but may help people
// to record what others thing,
// or why the change is proposed.
// Sometimes old notes can be used
// to help resolve disputes
// about interpretation
class Note {
  isA VisibilityControlledItem;
  
  // A note is about something
  // a note can even be about another
  // note, such as a private
  // observation about some rationale
  * -- 1 VisibilityControlledItem
    subject;
  
  // Arbitrary text associated with
  // the item
  markdownText;
  
  // By default there is no need
  // to record a note type, so it
  // would be Unknown. But if the note
  // provides evidence or justification
  // it can be tagged as Rationale,
  // and if the other party has reacted
  // in some way, it can be tagged
  // as FeedbackFromOtherParty to
  // allow for better searches
  enum NoteType {
    Unknown,
    Rationale,
    FeedbackFromOtherParty,
    Other
  }
  NoteType noteType;
  
  displayColor goldenrod;
}


// A Proposal is a set of ItemVersions
// that can be linked by 'and', 'or' or
// 'excusive or'
// 
// It represents ideas for changes to
// an agreement
//
// The simplest case could be a change
// to just one clause
//
// But many Proposals have multiple
// elements, which might be linked
// as follows
//
// Most sets of elements are linked
// by 'and' meaning a conjunct
//   a conjunct means that all 
//   changes are requested to be made
//   together
// 
// But some elements could be
// alternatives with exclusive or
// 'either this or that,
// but not both'.
//
// Others could be 'disjuncts'.
// 'Any of these ... it does
// not have to be all'
//
// A final approved proposal has to 
// be a conjunct, or just hava a single
// element

class Proposal {
  isA Version;
  
  // This enum describes the kind of
  // Proposal
  enum BooleanLink {
    conjunct,
    alternative,
    disjunct
  };
  BooleanLink booleanLink;
    
  // The elements can be
  // ClauseVersions or
  // Proposals (i.e. sub-proposals)
  0..1 -> * Version elements;
  
  displayColor cornflowerblue;
}

class Version { 
  isA VisibilityControlledItem;
  abstract;
  
  Date versionDate;
  Time versionTime;
  
  // Each version is a result of
  // changes to a previous version
  * -- 0..1 Version basedOn;

  displayColor lightsalmon;
}//$?[End_of_model]$?

namespace -;


class Agreement
{
  position 28 367 147.14674377441406 58.043479919433594;
  position.association Agreement__AgreementVersion 118,58 30,0;
  position.association Agreement__Party 72,0 74,41;
}

class LegalEntity
{
  position 171 4 147.75 92.953125;
}

class Party
{
  position 30 157 109 41.39945602416992;
  position.association Party__User 109,5 0,24;
}

class User
{
  position 178 150 113.19293975830078 74.6875;
}

class ClauseVersion
{
  position 590 520 113.78125 75.953125;
}

class VisibilityControlledItem
{
  position 388 146 155.078125 57.953125;
  position.association LegalEntity:allowedToSee__VisibilityControlledItem 123,0 161,59;
  position.association User:author__VisibilityControlledItem 0,4 113,30;
}

class Note
{
  position 118 266 169.14402770996094 74.6875;
  position.association Note__VisibilityControlledItem:subject 169,30 10,41;
}

class ItemVersion
{
  position 189 390 180.703125 91.953125;
  position.association ClauseVersion:containedClauses__ItemVersion:container 194,69 0,43;
}

class Clause
{
  position 574 251 145.25814819335938 73.6956558227539;
  position.association Clause__ClauseVersion 93,58 74,0;
}

class AgreementVersion
{
  position 157 518 142.34375 58.953125;
}

class Proposal
{
  position 465 391 190.74728393554688 58.043479919433594;
  position.association ItemVersion:elements__Proposal 0,12 194,12;
  position.association Proposal__Version:elements 150,0 140,76;
}

class Version
{
  position 414 260 145.1494598388672 74.6875;
}